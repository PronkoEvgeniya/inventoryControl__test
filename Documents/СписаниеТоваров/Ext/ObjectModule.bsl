
Процедура ОбработкаПроведения(Отказ, Режим)
	
	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СписаниеТоваровТовары.Номенклатура КАК Номенклатура,
		|	СУММА(СписаниеТоваровТовары.Количество) КАК Количество
		|ПОМЕСТИТЬ ТекДок
		|ИЗ
		|	Документ.СписаниеТоваров.Товары КАК СписаниеТоваровТовары
		|ГДЕ
		|	СписаниеТоваровТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	СписаниеТоваровТовары.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиТоваровНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиТоваровНаСкладахОстатки.Склад КАК Склад,
		|	ОстаткиТоваровНаСкладахОстатки.Партия КАК Партия,
		|	ЕСТЬNULL(ОстаткиТоваровНаСкладахОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ОстаткиТоваровНаСкладахОстатки.СуммаОстаток КАК СуммаОстаток
		|ПОМЕСТИТЬ Остатки
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровНаСкладах.Остатки(&МоментВремени, ) КАК ОстаткиТоваровНаСкладахОстатки
		|ГДЕ
		|	ОстаткиТоваровНаСкладахОстатки.Склад = &Склад
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТекДок.Номенклатура КАК Номенклатура,
		|	ТекДок.Количество КАК Количество,
		|	Остатки.Склад КАК Склад,
		|	Остатки.Партия КАК Партия,
		|	ЕСТЬNULL(Остатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	Остатки.СуммаОстаток КАК СуммаОстаток
		|ИЗ
		|	ТекДок КАК ТекДок
		|		ЛЕВОЕ СОЕДИНЕНИЕ Остатки КАК Остатки
		|		ПО ТекДок.Номенклатура = Остатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура,
		|	Партия
		|ИТОГИ
		|	МИНИМУМ(Количество),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Номенклатура";
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка); 
	Запрос.УстановитьПараметр("Склад", Склад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
		
			ФактичОстаток = ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток;
		    Сообщить("На складе не хватает " + ФактичОстаток + " шт. " + ВыборкаНоменклатура.Номенклатура + ". Фактический остаток: " + ВыборкаНоменклатура.КоличествоОстаток);	
		    Отказ = Истина;
			
		КонецЕсли;
		
		Если НЕ Отказ Тогда
		
			ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
			
			СчетчикСписания = ВыборкаНоменклатура.Количество;
	       
			Пока ВыборкаДетальныеЗаписи.Следующий() И СчетчикСписания > 0 Цикл 
				
				Списать = Мин(СчетчикСписания, ВыборкаДетальныеЗаписи.КоличествоОстаток);
				
				
				
				Движение = Движения.ОстаткиТоваровНаСкладах.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Период = Дата;
				Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
				Движение.Склад = Склад;
				Движение.Количество = Списать; 
				Движение.Партия = ВыборкаДетальныеЗаписи.Партия;
				СчетчикСписания = СчетчикСписания - Списать;
				Движение.Сумма = (ВыборкаДетальныеЗаписи.СуммаОстаток / ВыборкаДетальныеЗаписи.КоличествоОстаток) * Списать;
				
			КонецЦикла;	
			
		КонецЕсли;	
		 
	КонецЦикла;
	
	Движения.ОстаткиТоваровНаСкладах.Записывать = Истина;
	
КонецПроцедуры  


